Sub TransposerDonneesSelectionnees()

    Dim wsSource As Worksheet
    Dim wsDest As Worksheet
    Dim lastRow As Long
    Dim i As Long
    Dim destCol As Long
    Dim destRow As Long
    Dim maxCol As Long
    Dim currentCaseModule As String ' Variable pour suivre le module actuel

    maxCol = 15 ' Colonne O

    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual
    Application.DisplayAlerts = False

    Set wsSource = ThisWorkbook.Sheets("Source")

    ' Supprimer la feuille destination si elle existe
    On Error Resume Next
    Set wsDest = ThisWorkbook.Sheets("Transposé")
    If Not wsDest Is Nothing Then
        wsDest.Delete
    End If
    On Error GoTo 0

    Set wsDest = ThisWorkbook.Sheets.Add(After:=ThisWorkbook.Sheets(ThisWorkbook.Sheets.Count))
    wsDest.Name = "Transposé"

    wsDest.Columns("A:A").Insert Shift:=xlToRight
    wsDest.Columns("A:A").ColumnWidth = 3

    lastRow = wsSource.Cells(wsSource.Rows.Count, "A").End(xlUp).Row

    destCol = 2 ' Colonne B
    destRow = 1
    currentCaseModule = ""

    For i = 1 To lastRow

        If wsSource.Cells(i, 8).Value <> "" And wsSource.Cells(i, 8).Value <> currentCaseModule Then
            currentCaseModule = wsSource.Cells(i, 8).Value
            If destCol > 2 Then
                destCol = 2
                destRow = destRow + 4
            End If
        End If

        If destCol > maxCol + 1 Then
            destCol = 2
            destRow = destRow + 4
        End If

        ' --- Forcer le format texte pour chaque cellule transposée ---

        With wsDest.Cells(destRow, destCol)
            .NumberFormat = "@"
            .Formula = "='" & wsSource.Cells(i, 1).Text & "'" ' NUM ext
        End With

        With wsDest.Cells(destRow + 1, destCol)
            .NumberFormat = "@"
            .Formula = "='" & wsSource.Cells(i, 4).Text & "'" ' OBSERVATIONS
        End With

        With wsDest.Cells(destRow + 2, destCol)
            .NumberFormat = "@"
            .Formula = "='" & wsSource.Cells(i, 2).Text & "'" ' VOIE
            .Interior.Color = wsSource.Cells(i, 2).Interior.Color
            .Font.Color = wsSource.Cells(i, 2).Font.Color
        End With

        ' Colonne A = Case Module (vertical)
        With wsDest.Cells(destRow, 1)
            .NumberFormat = "@"
            .Value = currentCaseModule
            .Orientation = 90
            .Font.Bold = True
            .Interior.Color = RGB(230, 230, 230)
        End With

        ' Bordures
        With wsDest.Range(wsDest.Cells(destRow, destCol), wsDest.Cells(destRow + 2, destCol)).Borders
            .LineStyle = xlContinuous
            .ColorIndex = 0
            .TintAndShade = 0
            .Weight = xlThin
        End With

        destCol = destCol + 1
    Next i

    wsDest.Rows("1:8").Delete

    Application.ScreenUpdating = True
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True

    Call FusionnerCellulesMemeLigne
    Call AjusterDimensions
    Call CentrerCellules
    Call FusionnerCellulesCase
    Call ConfigurerMiseEnPage

    MsgBox "Création du cahier de tournée terminée!"

End Sub
